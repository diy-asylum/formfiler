buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.16.0"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3"
        classpath "com.palantir:jacoco-coverage:0.4.0"
    }
}
plugins {
    id 'java'
    id 'com.gradle.build-scan' version '2.1'
    id "com.github.kt3k.coveralls" version '2.8.2'
    id "com.palantir.jacoco-full-report" version "0.4.0"
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

subprojects {

    apply plugin: "java"
    apply plugin: "com.diffplug.gradle.spotless"
    apply plugin: "jacoco"

    sourceCompatibility = 11
    group 'com.diyasylum'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }
    test {
        useJUnitPlatform()
    }
    spotless {
        format 'misc', {
            target '*.gradle',
                    '*.md',
                    '.gitignore'
            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
        java {
            googleJavaFormat()
        }
        format 'styling', {
            target 'src/**/*.css',
                    'src/**/*.scss'
            prettier().config(['parser': 'css'])
        }
        format 'javascript', {
            target 'config/**/*.js',
                    'public/**/*.js',
                    'config/**/*.js',
                    'scripts/**/*.js',
                    'src/**/*.js',
                    '*.js'
            prettier().config(['parser': 'babylon'])
        }
    }

    jacoco {
        toolVersion = "0.8.2"
    }
    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled true
        }
    }
    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.8
                }
            }
        }
    }
}
coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoFullReport/jacocoFullReport.xml"
}

tasks.coveralls {
    doFirst {
        coveralls.sourceDirs = subprojects.sourceSets.main.java.srcDirs.flatten()
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishAlways()
}